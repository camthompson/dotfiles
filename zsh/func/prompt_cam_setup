function coalesce {
  for arg in $argv; do
    print "$arg"
    return 0
  done
  return 1
}

function prompt_cam_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_cam_pwd="$MATCH"
    unset MATCH
  else
    _prompt_cam_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_cam_precmd {
  prompt_cam_pwd

  git-info
}

function prompt_cam_setup {
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_cam_precmd

  zstyle ':keymap:insert' format ' %b%f>'
  zstyle ':keymap:overwrite' format ' %F{red}>'
  zstyle ':keymap:command' format ' %b%f<'
  zstyle ':git:action' format ':%F{red}%s'
  zstyle ':git:added' format ' %F{red}+'
  zstyle ':git:ahead' format ' %F{red}>'
  zstyle ':git:behind' format ' %F{red}<'
  zstyle ':git:branch' format '%F{magenta}%b'
  zstyle ':git:commit' format '%F{green}%.7c'
  zstyle ':git:deleted' format ' %F{red}x'
  zstyle ':git:modified' format ' %F{red}*'
  zstyle ':git:position' format ':%F{red}%p'
  zstyle ':git:renamed' format ' %F{red}R'
  zstyle ':git:stashed' format ' %F{red}='
  zstyle ':git:unmerged' format ' %F{red}§'
  zstyle ':git:untracked' format ' %F{red}?'
  zstyle ':git:keys' format \
    'prompt' "%A%B%S%a%d%m%r%U%u $(coalesce "%b" "%p" "%c")%s"

  # Define prompts.
  PROMPT='%F{cyan}${_prompt_cam_pwd}'
  PROMPT+='${editor_info[keymap]} %f%b'
  RPROMPT='${git_info[prompt]}%f%b'
}

prompt_cam_setup "$@"
# vim:set ft=zsh:
