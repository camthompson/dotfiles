# tmux attach or new-session integration for zsh.

## Copyright (c) 2011, Frank Terbeck <ft@bewatermyfriend.org>
##
## Permission to use, copy, modify, and/or distribute this software for any
## purpose with or without fee is hereby granted, provided that the above
## copyright notice and this permission notice appear in all copies.
##
## THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
## WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
## MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
## ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
## WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
## ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
## OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

emulate -L zsh

local -A opt
zparseopts -D -M -A opt d x

: "${opt[@]}"
if (( ARGC > 1 )); then
    cat <<EOF
usage: t [OPTION(s)] <session-name>

\`session-name' may be an existing session name, a new session name,
or blank. If it is an existing session's name, the session is attached
to; otherwise, a new session is registered with tmux and then attached to.
If it is blank, the session name will be the current directory.

Options:
  -d    When attaching to an existing session, pass the \`-d' option
        to tmux, which will detach any other clients attached to the
        session.
  -x    Call "tmux attach" using \`exec', but without. So,
        when present and the tmux session is detached from again,
        control will be picked up be the (still existing) parent shell
        again.

Use tab completion to complete option characters and names of existing
sessions.
EOF
    if [[ -z $TMUX ]]; then
        return 1
    else
        print
    fi
fi

if [[ -n $TMUX ]]; then
    cat <<EOF
Environment variable \`\$TMUX' set. \`t' will not attempt to nest.
EOF
    return 1
fi

local tmp session
local -i rc

if [[ -n $1 ]]; then
  session=$1
  shift
else
  session="${${PWD/$HOME/home}:t}"
fi
local -a tmux_options

tmux_options=(new-session -As $session)
(( ${+opt[-d]} )) && tmux_options+=(-d)

local EXEC=
(( ${+opt[-x]} )) && EXEC=exec

${=EXEC} tmux "${tmux_options[@]}"
